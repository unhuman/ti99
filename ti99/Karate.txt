1 REM  KARATE FOR TWO PLAYERS
2 CALL CHARPAT(ASC("!"),A$) :: IF A$<>"0000000000003030" THEN DISPLAY AT(12,1)ERASE ALL:"IMPROPER LOADING-PLEASE WAIT" :: RUN "DSK1.KLOAD"
7 DISPLAY AT(24,1):"PROGRAM LOADEDZ PLEASE WAIT!" :: CALL SOUND(300,110,0,220,0,330,0)
8 FOR DE=1 TO 200 :: NEXT DE
9 CALL MAGNIFY(4)
10 REGULARG=96 :: REGULARB=100 :: LOWKICKG=104 :: LOWKICKB=108 :: MEDKICKG=112 :: MEDKICKB=116 :: HIGHPUNCHG=120 :: HIGHPUNCHB=124
11 MEDPUNCHG=60 :: MEDPUNCHB=92 :: HIT=40 :: MOUND=44 :: STAR=36 :: FIGHT=1 :: WING,WINB=0 :: STRENGTHG,STRENGTHB=7 :: BF,SCG,SCB=0 :: CALL CLEAR
12 CALL COLOR(12,2,2) :: FOR I=10 TO 22 :: CALL HCHAR(I,3,124,28) :: NEXT I :: CALL COLOR(11,11,11) :: CALL HCHAR(9,3,116,28) :: CALL HCHAR(23,3,116,28)
13 CALL COLOR(10,3,3)
14 DISPLAY AT(1,10):"# KARATE #" :: DISPLAY AT(3,3):"PLAYER 1:" :: DISPLAY AT(3,17):"PLAYER 2:"
15 DISPLAY AT(5,3):RPT$("h",STRENGTHG) :: DISPLAY AT(5,17):RPT$("h",STRENGTHB) :: DISPLAY AT(6,2):SCG :: DISPLAY AT(6,16):SCB
16 DISPLAY AT(7,2)SIZE(2):WING :: DISPLAY AT(7,16)SIZE(2):WINB :: DISPLAY AT(8,10):"FIGHT:";FIGHT
17 CALL SPRITE(#2,REGULARG,5,110,50,#3,REGULARB,7,110,168,#1,HIT,11,193,1) :: GLOC=50 :: BLOC=168
18 DISPLAY AT(24,1)BEEP:"PRESS BOTH BUTTONS TO START!"
19 CALL KEY(1,K,S) :: CALL KEY(2,K1,S1) :: IF K=18 AND K1=18 THEN 20 ELSE 19
20 DISPLAY AT(24,1):"" :: CALL SOUND(100,400,0) :: CALL SOUND(100,200,0) :: CALL SOUND(100,600,0) :: CALL SOUND(100,300,0) :: CALL SOUND(100,150,0)
21 REM  GAME!
22 CALL JOYST(1,XG,YG) :: CALL KEY(1,KG,SG) :: CALL JOYST(2,XB,YB) :: CALL KEY(2,KB,SB)
23 IF KG<>18 THEN GLOC=GLOC+(XG*1.5)
24 IF GLOC<30 THEN GLOC=30
25 IF KB<>18 THEN BLOC=BLOC+(XB*1.5)
26 IF BLOC>204 THEN BLOC=204
27 IF GLOC>BLOC-12 THEN GLOC=GLOC-6 :: BLOC=BLOC+6
28 CALL LOCATE(#2,110,GLOC,#3,110,BLOC)
29 IF BF=0 THEN 33
30 IF KG=18 THEN GOSUB 36
31 IF KB=18 THEN GOSUB 49
32 GOTO 22
33 IF KB=18 THEN GOSUB 49
34 IF KG=18 THEN GOSUB 36
35 GOTO 22
36 REM  GOOD GUY HIT BAD
37 IF XG<>0 AND YG<>0 THEN 38 ELSE RETURN
38 IF XG=-4 AND YG=4 THEN CHAR=HIGHPUNCHG :: VERT=100
39 IF XG=4 AND YG=4 THEN CHAR=MEDKICKG :: VERT=110
40 IF XG=-4 AND YG=-4 THEN CHAR=MEDPUNCHG :: VERT=110
41 IF XG=4 AND YG=-4 THEN CHAR=LOWKICKG :: VERT=120
42 CALL PATTERN(#2,CHAR) :: CALL COINC(ALL,A) :: IF A THEN 44
43 FOR I=0 TO 28 STEP 4 :: CALL SOUND(-50,-2,I) :: NEXT I :: FOR DE=1 TO 10 :: NEXT DE :: BF=0 :: CALL PATTERN(#2,REGULARG) :: RETURN
44 CALL LOCATE(#1,VERT,BLOC-4) :: CALL SOUND(10,110,0,-5,0) :: STRENGTHB=STRENGTHB-1 :: SCG=SCG+10
45 FOR DE=1 TO 25 :: NEXT DE :: CALL LOCATE(#1,193,1) :: CALL PATTERN(#2,REGULARG)
46 DISPLAY AT(5,17)SIZE(7):RPT$("h",STRENGTHB) :: DISPLAY AT(6,2):SCG; :: IF STRENGTHB=0 THEN 62
47 BLOC=BLOC+12 :: CALL LOCATE(#3,110,BLOC)
48 RETURN
49 REM  BAD GUY HIT GOOD
50 IF XB<>0 AND YB<>0 THEN 51 ELSE RETURN
51 IF XB=4 AND YB=4 THEN CHAR=HIGHPUNCHB :: VERT=100
52 IF XB=-4 AND YB=4 THEN CHAR=MEDKICKB :: VERT=110
53 IF XB=4 AND YB=-4 THEN CHAR=MEDPUNCHB :: VERT=110
54 IF XB=-4 AND YB=-4 THEN CHAR=LOWKICKB :: VERT=120
55 CALL PATTERN(#3,CHAR) :: CALL COINC(ALL,A) :: IF A THEN 57
56 FOR I=0 TO 28 STEP 4 :: CALL SOUND(-50,-2,I) :: NEXT I :: FOR DE=1 TO 10 :: NEXT DE :: BF=1 :: CALL PATTERN(#3,REGULARB) :: RETURN
57 CALL LOCATE(#1,VERT,GLOC+4) :: CALL SOUND(10,110,0,-5,0) :: STRENGTHG=STRENGTHG-1 :: SCB=SCB+10
58 FOR DE=1 TO 25 :: NEXT DE :: CALL LOCATE(#1,193,1) :: CALL PATTERN(#3,REGULARB)
59 DISPLAY AT(5,3)SIZE(7):RPT$("h",STRENGTHG) :: DISPLAY AT(6,16):SCB; :: IF STRENGTHG=0 THEN 69
60 GLOC=GLOC-12 :: CALL LOCATE(#2,110,GLOC)
61 RETURN
62 FOR I=1 TO 8 :: CALL SOUND(-999,110,0) :: CALL PATTERN(#3,REGULARG) :: CALL SOUND(-999,220,0) :: CALL PATTERN(#3,REGULARB) :: NEXT I
63 CALL SOUND(-1,44000,30) :: CALL PATTERN(#3,MOUND) :: FOR DE=1 TO 250 :: NEXT DE :: CALL DELSPRITE(#3)
64 FOR I=STRENGTHG TO 1 STEP-1 :: SCG=SCG+10 :: DISPLAY AT(5,3)SIZE(7):RPT$("h",I-1); :: DISPLAY AT(6,2):SCG; :: CALL SOUND(10,I*110+110,0) :: NEXT I
65 WING=WING+1 :: DISPLAY AT(7,2)SIZE(2):WING; :: FIGHT=FIGHT+1 :: GOSUB 94 :: IF FIGHT=8 THEN 76
66 STRENGTHG=7 :: STRENGTHB=7
67 DISPLAY AT(5,3)SIZE(7):RPT$("h",STRENGTHG) :: DISPLAY AT(5,17)SIZE(7):RPT$("h",STRENGTHB) :: DISPLAY AT(6,2):SCG; :: DISPLAY AT(6,16):SCB
68 GOTO 16
69 FOR I=1 TO 8 :: CALL SOUND(-999,110,0) :: CALL PATTERN(#2,REGULARG) :: CALL SOUND(-999,220,0) :: CALL PATTERN(#2,REGULARB) :: NEXT I
70 CALL SOUND(-1,44000,30) :: CALL PATTERN(#2,MOUND) :: FOR DE=1 TO 250 :: NEXT DE :: CALL DELSPRITE(#2)
71 FOR I=STRENGTHB TO 1 STEP-1 :: SCB=SCB+10 :: DISPLAY AT(5,17)SIZE(7):RPT$("h",I-1) :: DISPLAY AT(6,16):SCB :: CALL SOUND(10,I*110+110,0) :: NEXT I
72 WINB=WINB+1 :: DISPLAY AT(7,16)SIZE(2):WING :: FIGHT=FIGHT+1 :: GOSUB 122 :: IF FIGHT=8 THEN 76
73 STRENGTHG=7 :: STRENGTHB=7
74 DISPLAY AT(5,3)SIZE(7):RPT$("h",STRENGTHG) :: DISPLAY AT(5,17)SIZE(7):RPT$("h",STRENGTHB) :: DISPLAY AT(6,2):SCG; :: DISPLAY AT(6,16):SCB
75 GOTO 16
76 REM  game over
77 CALL DELSPRITE(ALL) :: FOR I=9 TO 24 :: DISPLAY AT(I,1):"" :: NEXT I :: IF SCG>SCB THEN WINP$="PLAYER 1 WON BY POINTS"
78 IF SCB>SCG THEN WINP$="PLAYER 2 WON BY POINTS"
79 IF SCG=SCB THEN WINP$="NO WINNER BY POINTS"
80 IF WING>WINB THEN WINM$="PLAYER 1 WON BY MATCHES"
81 IF WINB>WING THEN WINM$="PLAYER 2 WON BY MATCHES"
82 IF WINB>WING AND SCB>SCG THEN TOTAL$="PLAYER 2 IS THE CLEAR WINNER" :: GOTO 85
83 IF WING>WINB AND SCG>SCB THEN TOTAL$="PLAYER 1 IS THE CLEAR WINNER" :: GOTO 85
84 TOTAL$="NO CLEAR WINNER"
85 DISPLAY AT(10,10):"SUMMARY:" :: DISPLAY AT(13,(28-LEN(WINP$))/2):WINP$
86 DISPLAY AT(14,(28-LEN(WINM$))/2):WINM$
87 DISPLAY AT(17,(28-LEN(TOTAL$))/2):TOTAL$
88 DISPLAY AT(21,9):"GAME  OVER" :: DISPLAY AT(22,5)BEEP:"PLAY AGAINQQ Y OR N"
89 CALL KEY(3,K,S) :: IF S=0 THEN 89
90 IF K=78 THEN CALL CLEAR :: STOP
91 IF K=89 THEN CALL CLEAR :: GOTO 10
92 CALL SOUND(100,110,0,220,0,330,0)
93 GOTO 89
94 REM  GOOD GUY BONUS SCREEN
95 CALL LOCATE(#2,110,112) :: CALL SPRITE(#10,REGULARG,2,110,92)
96 SKILL=9
97 B(4)=INT(RND*3) :: B(5)=INT(RND*3)
98 FIRST=4 :: CALL SPRITE(#4,STAR,7,B(4)*12+97,210,#5,STAR,7,B(5)*12+97,256)
99 CALL SOUND(250,247,0) :: CALL SOUND(250,220,0) :: CALL SOUND(500,247,0)
100 CALL MOTION(#4,0,-SKILL,#5,0,-SKILL)
101 CALL JOYST(1,X,Y) :: CALL KEY(1,K,S) :: IF X*Y<>0 AND K=18 THEN 109
102 CALL PATTERN(#4,HIT,#5,HIT) :: CALL COINC(ALL,A) :: IF A THEN 105
103 CALL PATTERN(#4,STAR,#5,STAR)
104 GOTO 101
105 CALL DELSPRITE(#4,#5,#10)
106 FOR I=1 TO 8 :: CALL SOUND(-999,110,0) :: CALL PATTERN(#2,REGULARG) :: CALL SOUND(-999,220,0) :: CALL PATTERN(#2,REGULARB) :: NEXT I
107 CALL SOUND(-1,44000,30) :: CALL PATTERN(#2,MOUND) :: FOR DE=1 TO 250 :: NEXT DE :: CALL DELSPRITE(#2)
108 RETURN
109 REM  TRY TO HIT STARS
110 IF X=-4 AND Y=4 THEN CHAR=HIGHPUNCHG :: BH=0
111 IF X=4 AND Y=4 THEN CHAR=MEDKICKG :: BH=1
112 IF X=-4 AND Y=-4 THEN CHAR=MEDPUNCHG :: BH=1
113 IF X=4 AND Y=-4 THEN CHAR=LOWKICKG :: BH=2
114 CALL PATTERN(#2,CHAR) :: FOR I=1 TO 8 :: CALL COINC(ALL,A) :: IF A THEN 117
115 NEXT I :: FOR I=0 TO 28 STEP 4 :: CALL SOUND(-50,-2,I) :: NEXT I :: FOR DE=1 TO 10 :: NEXT DE :: BF=0 :: CALL PATTERN(#2,REGULARG)
116 GOTO 101
117 IF BH<>B(FIRST)THEN 105
118 CALL SOUND(10,110,0,-5,0) :: CALL DELSPRITE(#FIRST) :: SCG=SCG+5 :: FIRST=FIRST+1 :: DISPLAY AT(6,2):SCG;
119 CALL PATTERN(#2,REGULARG)
120 IF FIRST=6 THEN SKILL=SKILL+2 :: GOTO 97
121 GOTO 101
122 REM  BAD GUY BONUS SCREEN
123 CALL LOCATE(#3,110,112) :: CALL SPRITE(#10,REGULARB,2,110,132)
124 SKILL=9
125 B(5)=INT(RND*3) :: B(4)=INT(RND*3)
126 FIRST=5 :: CALL SPRITE(#4,STAR,7,B(4)*12+97,230,#5,STAR,7,B(5)*12+97,20)
127 CALL SOUND(250,247,0) :: CALL SOUND(250,220,0) :: CALL SOUND(500,247,0)
128 CALL MOTION(#5,0,SKILL,#4,0,SKILL)
129 CALL JOYST(2,X,Y) :: CALL KEY(2,K,S) :: IF X*Y<>0 AND K=18 THEN 137
130 CALL PATTERN(#4,HIT,#5,HIT) :: CALL COINC(ALL,A) :: IF A THEN 133
131 CALL PATTERN(#4,STAR,#5,STAR)
132 GOTO 129
133 CALL DELSPRITE(#4,#5,#10)
134 FOR I=1 TO 8 :: CALL SOUND(-999,110,0) :: CALL PATTERN(#3,REGULARG) :: CALL SOUND(-999,220,0) :: CALL PATTERN(#3,REGULARB) :: NEXT I
135 CALL SOUND(-1,44000,30) :: CALL PATTERN(#3,MOUND) :: FOR DE=1 TO 250 :: NEXT DE :: CALL DELSPRITE(#3)
136 RETURN
137 REM  TRY TO HIT STARS
138 IF X=4 AND Y=4 THEN CHAR=HIGHPUNCHB :: BH=0
139 IF X=4 AND Y=-4 THEN CHAR=MEDPUNCHB :: BH=1
140 IF X=-4 AND Y=4 THEN CHAR=MEDKICKB :: BH=1
141 IF X=-4 AND Y=-4 THEN CHAR=LOWKICKB :: BH=2
142 CALL PATTERN(#3,CHAR) :: FOR I=1 TO 8 :: CALL COINC(ALL,A) :: IF A THEN 145
143 NEXT I :: FOR I=0 TO 28 STEP 4 :: CALL SOUND(-50,-2,I) :: NEXT I :: FOR DE=1 TO 10 :: NEXT DE :: BF=0 :: CALL PATTERN(#3,REGULARB)
144 GOTO 129
145 IF BH<>B(FIRST)THEN 133
146 CALL SOUND(10,110,0,-5,0) :: CALL DELSPRITE(#FIRST) :: SCB=SCB+5 :: FIRST=FIRST-1 :: DISPLAY AT(6,16):SCB;
147 CALL PATTERN(#3,REGULARB)
148 IF FIRST=3 THEN SKILL=SKILL+2 :: GOTO 125
149 GOTO 129

