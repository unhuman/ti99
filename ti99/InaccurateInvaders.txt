1 CALL CLEAR :: CALL SCREEN(2) :: DIM C$(10) :: DIM A$(6) :: DIM Z(6) :: FOR A=0 TO 9 :: READ C$(A) :: NEXT A :: CALL MAGNIFY(3) :: FOR A=0 TO 7 :: READ D$
2 CALL CHAR(136+A,D$) :: NEXT A :: FOR A=0 TO 13 :: CALL COLOR(A,16,1) :: NEXT A :: DISPLAY AT(10,2):"** INACCURATE  INVADERS **"
3 DISPLAY AT(12,9):"2010 unHUman" :: DISPLAY AT(22,9):"NOT FOR SALE" :: DISPLAY AT(24,9):"PRESS ANY KEY"
4 CALL KEY(0,K,S) :: IF S=0 THEN 4 ELSE CALL CLEAR :: READ D$,E$,F$ :: CALL CHAR(120,D$) :: CALL CHAR(124,E$) :: CALL CHAR(128,F$) :: S=0 :: L=0
5 RANDOMIZE :: CALL CLEAR :: FOR A=1 TO 31 STEP 2 :: B=INT(RND*4) :: CALL HCHAR(24,A,136+(B*2)) :: CALL HCHAR(24,A+1,137+(B*2)) :: NEXT A
6 M=0 :: CALL COLOR(14,13,1) :: CALL DELSPRITE(ALL) :: CALL SPRITE(#3,128,7,192,256) :: RC=0 :: DISPLAY AT(1,23):USING "#####":S
7 Y=L :: X=1 :: D=1 :: RESTORE 36 :: FOR A=0 TO 5 :: READ C,A$(A) :: CALL COLOR(4+A,C,1) :: Z(A)=7 :: NEXT A
8 CALL SPRITE(#1,120,11,176,220) :: CALL SPRITE(#2,124,15,256,256,-10,0)
9 CALL LOCATE(#3,192,256) :: V=15 :: M=1-M :: IF X>14 OR X<1 THEN D=-D :: V=0 :: Y=Y+1 :: IF X>14 THEN X=14
10 FOR A=5 TO 1 STEP-1 :: IF Z(A)=0 THEN 19 ELSE CALL JOYST(1,P,Q) :: CALL MOTION(#1,0,P*4) :: H=Y+A*2 :: IF V=0 THEN DISPLAY AT(H-1,X):A$(0)
11 CALL POSITION(#1,P,Q) :: IF Q<20 THEN Q=20 ELSE IF Q>220 THEN Q=220 ELSE 13
12 CALL MOTION(#1,0,0) :: CALL LOCATE(#1,P,Q)
13 CALL CHAR(57+A*8,C$((A-1)*2+M)) :: DISPLAY AT(H,X):A$(A) :: IF H=23 THEN 26
14 CALL POSITION(#2,E,F) :: E=INT(E/8)+1 :: IF E<2 OR E>24 OR F>250 THEN CALL LOCATE(#2,256,256) :: GOTO 19
15 F=INT((F+2)/8)+1 :: CALL GCHAR(E,F,C) :: CALL GCHAR(E+1,F,C2) :: IF C=32 AND C2=32 THEN 19 ELSE IF C2<>32 THEN E=E+1
16 CALL LOCATE(#2,256,256) :: CALL LOCATE(#3,E*8-12,F*8-12) :: CALL HCHAR(E,F,32) :: CALL SOUND(-200,-7,10) :: ROW=INT((E+1-Y)/2) :: IF X<1 THEN X=1
17 COL=F-X-1 :: S=S+(6-ROW)*10 :: DISPLAY AT(1,23):USING "#####":S :: Z(ROW)=Z(ROW)-1 :: IF Z(ROW)<=0 THEN A$(ROW)="" :: RC=RC+1 :: GOTO 19
18 A$(ROW)=SEG$(A$(ROW),1,COL-1)&" "&SEG$(A$(ROW),COL+1,15) :: COL=COL+D :: A$(ROW)=SEG$(A$(ROW),1,COL-1)&" "&SEG$(A$(ROW),COL+1,15)
19 NEXT A :: CALL SOUND(-25,-3,V) :: X=X+D :: IF RC=5 THEN 24
20 CALL KEY(1,K,L) :: IF K<>18 THEN 23 ELSE CALL SOUND(-100,800,10,1100,15) :: CALL POSITION(#1,P,Q)
21 IF Q<24 THEN Q=24 :: CALL LOCATE(#1,P,Q)ELSE IF Q>223 THEN Q=223 :: CALL LOCATE(#1,P,Q)
22 CALL LOCATE(#2,180,Q+5)
23 GOTO 9
24 FOR A=1 TO 5 :: FOR B=110 TO 210 STEP 20 :: CALL SOUND(50,A*220+B,5) :: NEXT B :: NEXT A :: L=L+1 :: CALL DELSPRITE(ALL) :: IF L<13 THEN 5
25 CALL CLEAR :: CALL CHARSET :: PRINT "You Won!" :: PRINT "Your Score: "&STR$(S) :: PRINT "Bye Bye" :: END
26 CALL DELSPRITE(ALL) :: FOR A=1010 TO 110 STEP-50 :: CALL SOUND(50,A,5,-2,0) :: NEXT A :: FOR A=1 TO 15 :: CALL SCREEN(7) :: CALL SOUND(50,-2,A)
27 CALL SCREEN(2) :: CALL SOUND(50,-7,A) :: NEXT A :: CALL CLEAR :: CALL CHARSET :: FOR A=0 TO 13 :: CALL COLOR(A,15,1) :: NEXT A
28 DISPLAY AT(1,23):USING "#####":S :: DISPLAY AT(9,9):"GAME  OVER" :: DISPLAY AT(15,8):"AGAIN? (Y/N)" :: L=0 :: S=0
29 CALL KEY(0,K,S) :: IF K=ASC("Y")THEN 5 ELSE IF K<>ASC("N")THEN 29 ELSE CALL CLEAR :: CALL COLOR(14,1,1)
30 DATA 081C1C3E2A3E55AA,081C1C3E2A3E2A55,18BDA5BDFF7E187E,183C243CFFFF9999,3C76F3BFFC5C4477,3C6ECFFD3F3A22EE
31 DATA 3C7E997E18244281,3C7EE77E3C425A24,3CFFDBDBFF7E5A81,3CFFFFDBFF7E4224
32 DATA 00000000000043FF,000000000000C7FF,00000C0E9FFFFFFF,00000000C0F0FEFF
33 DATA 00000000074FFFFF,0000000090F8FEFF,00000000030FFFFF,00000000C0FCFEFF
34 DATA 00000000000103060E1D7FFFCEB5CE3F000000000080C06070B8FEFF73AD73FC,3078787830003030003000000000000000000000000000000000000000000000
35 DATA 00000C1838028040401900000F41400000000E5000004108E88818000C980000
36 DATA 15,"               ",14," A A A A A A A ",5," I I I I I I I ",6," Q Q Q Q Q Q Q ",8," Y Y Y Y Y Y Y ",4," a a a a a a a "

